\section{Solutions to the exercises}

\begin{itemize}
\item \textbf{Exercise 1}

\begin{lstlisting}
// Input: 
// - Hyperstack holding the 4 channels of the FISH assay (DAPI channel first)
// Output: 
// - Split channels, no scale

// Split channels
run("Split Channels");

// Initialization
run("Set Scale...", "distance=0 known=0 pixel=1 unit=pixel global");
run("Options...", "iterations=1 count=1 edm=Overwrite");
run("Set Measurements...", "area mean centroid shape redirect=None decimal=2");
\end{lstlisting}
\textbf{sourcecode}: mod3conversionTest/module3_01.ijm
\item \textbf{Exercise 2}

\begin{lstlisting}
// Input: 
// - 4 channels of the FISH experiment in 4 images: C1-Small.tif, C2-Small.tif, C3-Small.tif and C4-Small.tif
// Output: 
// - Detected nuclei in ROI manager

//Segment Nuclei
selectImage("C1-Small.tif");
run("FeatureJ Laplacian", "compute smoothing=12");
getMinAndMax(min,max);
setThreshold(min,-0.05);
run("Convert to Mask");
run("Fill Holes");
for(i=0;i<2;i++)run("Dilate");

//Split Particles
run("Watershed");
rename("Mask");

//Analyze Particles and store to ROI manager
run("Analyze Particles...", "size=1000-2500 circularity=0.75-1.00 show=Nothing display exclude clear include add");
NbNuclei = roiManager("count");

selectImage("Mask");
close();
selectImage("C1-Small.tif");
roiManager("Show All");
\end{lstlisting}
\textbf{sourcecode}: mod3conversionTest/module3_02simple.ijm
\item \textbf{Exercise 3}

\begin{lstlisting}
// Input: 
// - 4 channels of the FISH experiment in 4 images: C1-Small.tif, C2-Small.tif, C3-Small.tif and C4-Small.tif
// Output: 
// - Detected nuclei in ROI manager

//Segment Nuclei
selectImage("C1-Small.tif");
run("FeatureJ Laplacian", "compute smoothing=12");
getMinAndMax(min,max);
setThreshold(min,-0.05);
run("Convert to Mask");
run("Fill Holes");
for(i=0;i<2;i++)run("Dilate");

//Split Particles
run("Watershed");
rename("Mask");

//Analyze particle to estimate median area
run("Analyze Particles...", "size=0-Infinity circularity=0.75-1.00 show=Nothing display exclude clear include"); 
Area = newArray(nResults);
for(i=0;i<nResults;i++)Area[i] = getResult("Area", i);
Area = Array.sort(Area);
MedianArea = Area[nResults/2];
print("Median area: "+d2s(MedianArea,0));

//Analyze Particles and store to ROI manager
run("Analyze Particles...", "size="+MedianArea*0.66+"-"+MedianArea*1.5+" circularity=0.75-1.00 show=Nothing display exclude clear include add");
NbNuclei = roiManager("count");

selectImage("Mask");
close();
selectImage("C1-Small.tif");
roiManager("Show All");
\end{lstlisting}
\textbf{sourcecode}: mod3conversionTest/module3_02.ijm
\item \textbf{Exercise 4}

\begin{lstlisting}
// Input: 
// - ROI Manager with nuclei selection
// - C1-Small.tif, C2-Small.tif, C3-Small.tif and C4-Small.tif opened
// Output: 
// - Spot segmentation masks of the 3 channels
// - 1 Arrays with spot counted in each nucleus in channel 1

NbNuclei = roiManager("count");

// FISH spots detection
NbSpotsChan1 = newArray(NbNuclei);

// Pre-filtering
selectImage("C1-Small.tif");
run("FeatureJ Laplacian", "compute smoothing=3");
SpotLapID = getImageID();
	
// Spot segmentation
run("Find Maxima...", "noise=4 output=[Single Points] light");
SpotCandMaskID = getImageID();
	
// Spot count in each nucleus
selectImage(SpotCandMaskID);
for(j=0;j<NbNuclei;j++)
{ 	
	roiManager("select",j);
	getRawStatistics(nPixels, mean, min, max, std, histogram);
	NbSpots = histogram[255];
	NbSpotsChan1[j] = NbSpots;
}

// Display arrays with counted spots per nucleus
print("Array NbSpotsChan1:");
Array.print(NbSpotsChan1);

run("Select None");
\end{lstlisting}
\textbf{sourcecode}: mod3conversionTest/module3_03simple.ijm

\begin{lstlisting}
// Input: 
// - ROI Manager with nuclei selection
// - C1-Small.tif, C2-Small.tif, C3-Small.tif and C4-Small.tif opened
// Output: 
// - Spot segmentation masks of the 3 channels
// - 3 Arrays (on per channel) with spot counted in each nucleus

NbNuclei = roiManager("count");

// FISH spots detection
NbSpotsChan1 = newArray(NbNuclei);
NbSpotsChan2 = newArray(NbNuclei);
NbSpotsChan3 = newArray(NbNuclei);
for(i=1;i<4;i++)
{
	// Pre-filtering
	selectImage("C"+d2s(i+1,0)+"-Small.tif");
	run("FeatureJ Laplacian", "compute smoothing=3");
	SpotLapID = getImageID();
	
	// Spot segmentation
	run("Find Maxima...", "noise=4 output=[Single Points] light");
	SpotCandMaskID = getImageID();

	// Cleanup
	selectImage(SpotLapID);
	close();
	
	// Spot count in each nucleus
	selectImage(SpotCandMaskID);
	for(j=0;j<NbNuclei;j++)
	{ 	
		roiManager("select",j);
		getRawStatistics(nPixels, mean, min, max, std, histogram);
		NbSpots = histogram[255];
		if(i==1)NbSpotsChan1[j] = NbSpots;
		if(i==2)NbSpotsChan2[j] = NbSpots;
		if(i==3)NbSpotsChan3[j] = NbSpots;
	}	
}
run("Select None");

// Display arrays with counted spots per nucleus
print("Array NbSpotsChan1:");
Array.print(NbSpotsChan1);
print("Array NbSpotsChan2:");
Array.print(NbSpotsChan2);
print("Array NbSpotsChan3:");
Array.print(NbSpotsChan3);
\end{lstlisting}
\textbf{sourcecode}: mod3conversionTest/module3_03.ijm
\item \textbf{Exercise 5}

\begin{lstlisting}
// Input: 
// - ROI Manager with nuclei selection
// - C1-Small.tif, C2-Small.tif, C3-Small.tif and C4-Small.tif opened
// Output: 
// - Overlay of detected spots (channel colored)
// - Nuclei selection with color based on spot content

roiManager("Associate", "true");
NbNuclei = roiManager("count");

// FISH spots detection
NbSpotsChan1 = newArray(NbNuclei);
NbSpotsChan2 = newArray(NbNuclei);
NbSpotsChan3 = newArray(NbNuclei);
for(i=1;i<4;i++)
{	
	// Pre-filtering
	selectImage("C"+d2s(i+1,0)+"-Small.tif");
	run("FeatureJ Laplacian", "compute smoothing=3");
	SpotLapID = getImageID();
	
	// Spot segmentation
	run("Find Maxima...", "noise=4 output=[Single Points] light");
	SpotCandMaskID = getImageID();
	
	// Cleanup
	selectImage(SpotLapID);	
	close();

	// Spot count in each nucleus
	selectImage(SpotCandMaskID);
	for(j=0;j<NbNuclei;j++)
	{
		roiManager("select",j);
		getRawStatistics(nPixels, mean, min, max, std, histogram);
		NbSpots = histogram[255];
		if(i==1)NbSpotsChan1[j] = NbSpots;
		if(i==2)NbSpotsChan2[j] = NbSpots;
		if(i==3)NbSpotsChan3[j] = NbSpots;
	}
		
	// Spots overlay
	setThreshold(1,255);
	run("Create Selection");
	
	//Check if selection
	if(selectionType>-1)
	{
		run("Enlarge...", "enlarge=1 pixel");
		roiManager("add");
		roiManager("select",roiManager("count")-1);
		if(i==1)roiManager("Set Color", "red");
		if(i==2)roiManager("Set Color", "green");
		if(i==3)roiManager("Set Color", "blue");
		roiManager("Deselect");
	}
	selectImage(SpotCandMaskID);
	close();
}
	
// Draw color coded outlines of the nuclei
selectImage("C1-Small.tif");
for(j=0;j<NbNuclei;j++)
{
	roiManager("select",j);
	ColorCode = "FF"+toHex(63+64*NbSpotsChan1[j])+toHex(63+64*NbSpotsChan2[j])+toHex(63+64*NbSpotsChan3[j]);
	run("Properties... ", "name=Nuc"+d2s(j,0)+" stroke="+ColorCode+" width=1 fill=none");
	roiManager("update");
}
run("Images to Stack", "name=Stack title=[] use");
roiManager("Show All without labels");

// Associate spots to slice
for(j=2;j<=4;j++)
{
	roiManager("select",roiManager("count")-5+j);
	setSlice(j);
	roiManager("update");
}
setSlice(1)
roiManager("Show All without labels");
\end{lstlisting}
\textbf{sourcecode}: mod3conversionTest/module3_03-04.ijm
\end{itemize}
