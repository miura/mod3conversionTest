\begin{enumerate}
    \item Apply Laplacian of Gaussian. We pre-filter the image of the first FISH channel with \ijmenu{[Plugins > Feature Extraction > FeatureJ >  FeatureJ Laplacian]}. The smoothing scale should be adjusted to the size of the spots! 
    
    
    \item Detect intensity regional minima with \ijmenu{[Process > Find Maxima...]}. You should tick \textbf{"Light background"} to detect minima and adjust \textbf{"Noise tolerance"} to optimize detection. Select \textbf{"Single Points"} as \textbf{"Output Type"} to create a binary mask with detected minima.
    
    \item Counting spots inside nuclei.
    In Step 2 we have already segmented the nuclei and stored them to the ROI manager. Now, we simply need to select the binary mask holding the detect spots and loop through the nuclei ROIs. Next, we count the number of pixels with intensity equal to 255 to retrieve the number of spots per nucleus.
    
    For each nucleus we will measure the statistics of the intensity with the macro function \textbf{getRawStatistics(nPixels, mean, min, max, std, histogram)} which among other returns the histogram of the pixel intensity inside the active selection. 

\end{enumerate}

\subsection{Exercise \arabic{exerciseCounter}}
\stepcounter{exerciseCounter}
Complete the macro \textbf{code/module3\_03simple-incomplete.ijm} to perform the previous sequence on the three FISH channels. Before launching the macro you need to have \textbf{C2-Small.tif} opened and the detected nuclei stored in the ROI Manager.

Open, read, and test the macro. The macro automatically segments the FISH spots following the workflow we previously described but the part counting the spots in each nucleus is incomplete. Several tasks have to be performed:

\begin{enumerate}

\item Write the missing code to detect the intensity regional minima. If you perform it right you should see a list of values indicating the number of spots detected in each nucleus in the log window. Solution in \textbf{code/solutions/module3\_03simple.ijm}.

\item Modify the code to automatically repeat the same workflow on the other 2 channels. This time you need the 3 channels opened before running the code. Solution in \textbf{code/solutions/module3\_03.ijm}.
\end{enumerate}

\textbf{\underline{Hint}} : You will need a loop over the channels. To properly select the correct image at each iteration you can make use of string concatenation (the channel images are called \textbf{"C2-Small.tif"}, \textbf{"C3-Small.tif"} and \textbf{"C4-Small.tif"}).


\begin{lstlisting}[linerange={1-6}]
// Input: 
// - ROI Manager with nuclei selection
// - C1-Small.tif, C2-Small.tif, C3-Small.tif and C4-Small.tif opened
// Output: 
// - Spot segmentation masks of the 3 channels
// - 3 Arrays (on per channel) with spot counted in each nucleus

NbNuclei = roiManager("count");

// FISH spots detection
NbSpotsChan1 = newArray(NbNuclei);
NbSpotsChan2 = newArray(NbNuclei);
NbSpotsChan3 = newArray(NbNuclei);
for(i=1;i<4;i++)
{
	// Pre-filtering
	selectImage("C"+d2s(i+1,0)+"-Small.tif");
	run("FeatureJ Laplacian", "compute smoothing=3");
	SpotLapID = getImageID();
	
	// Spot segmentation
	run("Find Maxima...", "noise=4 output=[Single Points] light");
	SpotCandMaskID = getImageID();

	// Cleanup
	selectImage(SpotLapID);
	close();
	
	// Spot count in each nucleus
	selectImage(SpotCandMaskID);
	for(j=0;j<NbNuclei;j++)
	{ 	
		roiManager("select",j);
		getRawStatistics(nPixels, mean, min, max, std, histogram);
		NbSpots = histogram[255];
		if(i==1)NbSpotsChan1[j] = NbSpots;
		if(i==2)NbSpotsChan2[j] = NbSpots;
		if(i==3)NbSpotsChan3[j] = NbSpots;
	}	
}
run("Select None");

// Display arrays with counted spots per nucleus
print("Array NbSpotsChan1:");
Array.print(NbSpotsChan1);
print("Array NbSpotsChan2:");
Array.print(NbSpotsChan2);
print("Array NbSpotsChan3:");
Array.print(NbSpotsChan3);
\end{lstlisting}
\textbf{sourcecode}: mod3conversionTest/module3_03.ijm

\subsection{Summary of tools used in Step 3}

\begin{itemize}
\item\textbf{FeatureJ Laplacian} See step 2

\item\textbf{Threshold} See ``ImageJ Basics''.
\item\textbf{Analyze Particles} See ``ImageJ Basics''.
\item\textbf{ROI Manager} See ``ImageJ Basics''.

\end{itemize}

\newpage
